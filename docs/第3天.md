# 第3天 进入32位模式并导入C语言

## 制作真正的IPL

这次我们将使用**启动区**装载程序。

```nasm title="ipl.nas"
; haribote-ipl
; TAB=4

	ORG    0x7c00
	JMP    entry
	DB     0x90
	DB     "HARIBOTE"
	DW     512
	DB     1
	DW     1
	DB     2
	DW     224
	DW     2880
	DB     0xf0
	DW     9
	DW     18
	DW     2
	DD     0
	DD     2880
	DB     0,0,0x29
	DD     0xffffffff
	DB     "HARIBOTEOS "
	DB     "FAT12   "
	RESB   18

entry:
	MOV    AX,0
	MOV    SS,AX
	MOV    SP,0x7c00
	MOV    DS,AX

	MOV    AX,0x0820
	MOV    ES,AX
	MOV    CH,0         ; 柱面0
	MOV    DH,0         ; 磁头0
	MOV    CL,2         ; 扇区2
	MOV    AH,0x02      ; 读盘
	MOV    AL,1         ; 1个扇区
	MOV    BX,0
	MOV    DL,0x00      ; A驱动器
	INT    0x13         ; 调用磁盘BIOS
	JC     error
```

- `JC`: jump if carry。如果进位标志(carry flag)是1，就跳转。
- **标志**：1位寄存器

### `INT 0x13`

- 磁盘读、写，扇区校验，以及寻道
	- `AH=0x02` 读盘
	- `AH=0x03` 写盘
	- `AH=0x04` 校验
	- `AH=0x0c` 寻道
	- `AL=处理对象的扇区数(只能同时处理连续的扇区)`
	- `CH=柱面号 &0xff`
	- `CL=扇区号(0-5位) | (柱面号&0x300)**2`
	- `DH=磁头号`
	- `DL=驱动器号`
	- `ES:BX=缓冲地址(校验及寻道时不使用)`
	- 返回值：
	- `FLAGS.CF==0` 没有错误，`AH==0`
	- `FLAGS.CF==1` 有错误，错误号码存入`AH`内(与重置功能一样)

1张软盘有80个柱面，2个磁头，18个扇区，一个扇区有512字节。

含有IPL的启动区，位于`C0-H0-S1`(柱面0，磁头0，扇区1)

下一个扇区是`C0-H0-S2`

### 缓冲地址

 这是个内存地址，==表明我们要把从软盘上读出的数据装载到内存中的哪个位置。==

如果能用一个寄存器来表示内存地址的话，很方便，但是一个`BX`只能表示`0~0xffff`(16位)，也就是只有`0~65535`。

$$65535 \div 2^{10} = 63.999$$

最大才64K。但是电脑最起码也都有64M内存。

#### 段寄存器

我们在使用段寄存器时，以`ES:BX`这种方式来表示地址，写成
```nasm
MOV AL,[ES:BX]
```
它代表`ES*16+BX`的内存地址。我们可以理解成，==先用`ES`寄存器指定一个大致的地址，然后再用`BX`来指定其中一个具体地址==。这样就是$65535\times 17 =1114095字节=1087.983KB$，这就可以指定1M以内的内存了。

这次`ES=0x0820, BX=0`，所以软盘的数据将被装载到内存中`0x8200`到`0x83ff`。`0x8000~0x81ff`，要把启动区的内容读到那里。`0x7c00~0x7dff`用于启动区，`0x7e00~0x9fbff`没有特别的用途，操作系统可以随意使用。

#### 默认的段寄存器 `DS:`

```nasm
MOV  CX,[1234]
; 其实是
MOV  CX,[DS:1234]
```

**因为有这样的规则，所以`DS`必须预先设置为0**

### 使用变量的Makefile

```makefile
TOOLPATH = ../z_tools/
MAKE     = $(TOOLPATH)make.exe -r
NASK     = $(TOOLPATH)nask.exe
EDIMG    = $(TOOLPATH)edimg.exe
IMGTOL   = $(TOOLPATH)imgtol.com
COPY     = copy
DEL      = del

default :
	$(MAKE) img

ipl.bin : ipl.nas Makefile
	$(NASK) ipl.nas ipl.bin ipl.lst

haribote.img : ipl.bin Makefile
	$(EDIMG) imgin:../z_tools/fdimg0at.tek wbinimg src:ipl.bin len:512 from:0 to:0 imgout:haribote.img

asm : 
	$(MAKE) ipl.bin

img :
	$(MAKE) haribote.img

run :
	$(MAKE) img
	$(COPY) haribote.img ..\z_tools\qemu\fdimage0.bin
	$(MAKE) -C ../z_tools/qemu

install :
	$(MAKE) img
	$(IMGTOL) w a:haribote.img

clean :
	-$(DEL) ipl.bin
	-$(DEL) ipl.lst

src_only :
	$(MAKE) clean
	-$(DEL) haribote.img
```

## 试错

如果软盘不能读数据，我们决定重试5次。

```nasm title="ipl.nas"
; haribote-ipl
; TAB=4
	ORG    0x7c00
	JMP    entry
	DB     0x90
	DB     "HARIBOTE"
	DW     512
	DB     1
	DW     1
	DB     2
	DW     224
	DW     2880
	DB     0xf0
	DW     9
	DW     18
	DW     2
	DD     0
	DD     2880
	DB     0,0,0x29
	DD     0xffffffff
	DB     "HARIBOTEOS "
	DB     "FAT12   "
	RESB   18

entry:
	MOV    AX,0
	MOV    SS,AX
	MOV    SP,0x7c00
	MOV    DS,AX
	MOV    AX,0x0820
	MOV    ES,AX
	MOV    CH,0        ; 柱面0
	MOV    DH,0        ; 磁头0
	MOV    CL,2        ; 扇区2
	MOV    SI,0        ; 记录失败次数的寄存器

retry:
	MOV    AH,0x02
	MOV    AL,1
	MOV    BX,0
	MOV    DL,0x00     ; A驱动器
	INT    0x13        ; 调用磁盘BIOS
	JNC    fin         ; 没出错的话跳转到fin 
	ADD    SI,1
	CMP    SI,5
	JAE    error       ; SI>=5时，跳转到error
	MOV    AH,0x00
	MOV    DL,0x00     ; A驱动器
	INT    0x13        ; 重置驱动器
	JMP    retry

fin:
	HLT
	JMP    fin

error:
	MOV    SI,msg

putloop:
	MOV    AL,[SI]
	ADD    SI,1
	CMP    AL,0
	JE     fin
	MOV    AH,0x0e
	MOV    BX,15
	INT    0x10
	JMP    putloop

msg:
	DB     0x0a,0x0a
	DB     "load error"
	DB     0x0a
	DB     0
	RESB   0x7dfe-$
	DB     0x55,0xaa
```

- `JNC`: jump if not carry。进位标志是0的话就跳转。
- `JAE`: jump if above or equal。大于或等于时跳转。
- `AH=0x00, DL=0x00, INT 0x13`这是系统复位。

### 读到18扇区

```nasm title="ipl.nas"
	ORG    0x7c00
	JMP    entry
	DB     0x90
	DB     "HARIBOTE"
	DW     512
	DB     1
	DW     1
	DB     2
	DW     224
	DW     2880
	DB     0xf0
	DW     9
	DW     28
	DW     2
	DD     0
	DD     2880
	DB     0,0,0x29
	DD     0xffffffff
	DB     "HARIBOTEOS"
	DB     "FAT12   "
	RESB   18

	MOV    AX,0
	MOV    SS,AX
	MOV    SP,0x7c00
	MOV    DS,AX

	MOV    AX,0x0820
	MOV    ES,AX
	MOV    CH,0         ; 柱面0
	MOV    DH,0         ; 磁头0
	MOV    CL,2         ; 扇区2

readloop:
	MOV    SI,0         ; 记录失败次数的寄存器

retry:
	MOV    AH,0x02      ; 读入磁盘
	MOV    AL,1         ; 1个扇区
	MOV    BX,0         
	MOV    DL,0x00      ; A驱动器
	INT    0x13
	JNC    next         ; 没出错时跳转到next
	ADD    SI,1
	CMP    SI,5
	JAE    error        ; SI >= 5时,跳转到error
	MOV    AH,0x00
	MOV    DL,0x00      ; A驱动器
	INT    0x13         ; 重置驱动器
	JMP    retry

next:
	MOV    AX,ES        ; 把内存地址后移0x200
	ADD    AX,0x0020
	MOV    ES,AX        ; 因为没有ADD ES,0x0020的指令
	ADD    CL,1
	CMP    CL,18
	JBE    readloop     ; 如果CL <= 18跳转至readloop

fin:
	HLT
	JMP    fin

error:
	MOV    SI,msg

putloop:
	MOV    AL,[SI]
	ADD    SI,1
	CMP    AL,0
	JE     fin
	MOV    AH,0x0e
	MOV    BX,15
	INT    0x10
	JMP    putloop

msg:
	DB     0x0a,0x0a
	DB     "load error"
	DB     0x0a
	DB     0

	RESB   0x7dfe-$

	DB     0x55,0xaa
```

- `JBE`：jump if below or equal

程序要做的事很简单，要读下一个扇区，只需要给`CL`加1，给`ES`加上`0x20`就行了。`CL`是扇区号，`ES`指定读入地址。`0x20`是十六进制下512除以16的结果。

向`BX`里加上512确实更简单，但这次我们想练习往`ES`里面做加法的方法

## 读入10个柱面

`C0-H0-S18`的下一扇区，是**磁盘反面**的`C0-H1-S1`，这次也从`0xa400`读入，按顺序读到`C0-H1-S18`后，接着读下一个柱面`C1-H0-S1`，保持这个势头，一直读到`C9-H1-S18`就好了。

```nasm title="ipl.nas"
CYLS    EQU    10
		ORG    0x7c00

		JMP    entry
		DB     0x90
		DB     "HARIBOTE"
		DW     512
		DB     1
		DW     1
		DB     2
		DW     224
		DW     2880
		DB     0xf0
		DW     9
		DW     18
		DW     2
		DD     0
		DD     2880
		DB     0,0,0x29
		DD     0xffffffff
		DB     "HARIBOTEOS "
		DB     "FAT12   "
		RESB   "18"

entry:
		MOV    AX,0
		MOV    SS,AX
		MOV    SP,0x7c00
		MOV    DS,AX
		MOV    AX,0x0820
		MOV    ES,AX
		MOV    CH,0
		MOV    DH,0
		MOV    CL,2

readloop:
		MOV    SI,0

retry:
		MOV    AH,0x02
		MOV    AL,1
		MOV    BX,0
		MOV    DL,0x00
		INT    0x13
		JNC    next
		ADD    SI,1
		CMP    SI,5
		JAE    error
		MOV    AH,0x00
		MOV    DL,0x00
		INT    0x13
		JMP    retry

next:
		MOV    AX,ES
		ADD    AX,0x0020
		MOV    ES,AX
		ADD    CL,1
		CMP    CL,18
		JBE    readloop
		MOV    CL,1
		ADD    DH,1
		CMP    DH,2
		JB     readloop
		MOV    DH,0
		ADD    CH,1
		CMP    CH,CYLS
		JB     readloop

fin:
		HLT
		JMP    fin

error:
		MOV    SI,msg

putloop:
		MOV    AL,[SI]
		ADD    SI,1
		CMP    AL,0
		JE     fin
		MOV    AH,0x0e
		MOV    BX,15
		INT    0x10
		JMP    putloop

msg:
		DB     0x0a,0x0a
		DB     "load error"
		DB     0x0a
		DB     0
		RESB   0x7dfe-$
		DB     0x55,0xaa
```

- `EQU`：相当于C语言中的define。`EQU`的意思是equal

## 着手开发操作系统

我们先来编写一个非常短小的程序，就只让它`HLT`。

### 最简单的操作系统

```nasm title="haribote.nas"
fin:
	HLT
	JMP    fin
```

用nask编译输出为`haribote.sys`。接下来，将这个文件保存到**磁盘映像**`haribote.img`里。

操作：

- 使用`make install`命令，将磁盘映像文件写入磁盘
- 在Windows里打开那个磁盘，把`haribote.sys`保存到磁盘上
- 使用工具将磁盘备份为磁盘映像

我们再来想想一下，如果不用借助磁盘和Windows就可以得到磁盘映像和文件，那多方便啊😍。

能够做到这个的工具有很多，`edimg.exe`就是其中一个。

### `haribote.sys`

```makefile title="makefile"
TOOLPATH = ../z_tools/
MAKE     = $(TOOLPATH)make.exe -r
NASK     = $(TOOLPATH)nask.exe
EDIMG    = $(TOOLPATH)edimg.exe
IMGTOL   = $(TOOLPATH)imgtol.com
COPY     = copy
DEL      = del

default :
	$(MAKE) img

ipl.bin : ipl.nas Makefile
	$(NASK) ipl.nas ipl.bin ipl.lst

haribote.sys : haribote.nas Makefile
	$(NASK) haribote.nas haribote.sys haribote.lst

haribote.img : ipl.bin harbote.sys Makefile
	$(EDIMG) imgin:../z_tools/fdimg0at.tek wbinimg src:ipl.bin len:512 from:0 to:0 copy from:haribote.sys to:@: imgout:haribote.img

img :
	$(MAKE) haribote.img

run :
	$(MAKE) img
	$(COPY) haribote.img ..\z_tools\qemu\fdimage0.bin
	$(MAKE) -C ../z_tools/qemu

install :
	$(MAKE) img
	$(IMGTOL) w a: haribote.img

clean :
	-$(DEL) ipl.bin
	-$(DEL) ipl.lst
	-$(DEL) haribote.sys
	-$(DEL) haribote.lst

src_only :
	$(MAKE) clean
	-$(DEL) haribote.img
```

### `haribote.img`

用二进制编译器打开刚做成的映像文件`haribote.img`，**看一看`haribote.sys`文件在磁盘中是什么样的。**

最先注意到的地方是`0x002600`附近，磁盘的这个位置好像保存着文件名。

![image.png](https://vs-picbed-1320307070.cos.ap-nanjing.myqcloud.com/img/20240221195538.png)

再往下看，`0x004200`附近，可以看到`F4 EB FD`。

![image.png](https://vs-picbed-1320307070.cos.ap-nanjing.myqcloud.com/img/20240221195744.png)

这就是`haribote.sys`的内容。

![image.png](https://vs-picbed-1320307070.cos.ap-nanjing.myqcloud.com/img/20240221195837.png)

**总结**：

1. 文件名会写在`0x002600`以后的地方
2. 文件的内容会写在`0x004200`以后的地方

## 从启动区执行操作系统

要怎样才能执行磁盘映像上位于`0x4200`的程序🤔？

现在的程序是从启动区开始，把磁盘上的内容装载到内存`0x8000`号地址，所以磁盘`0x4200`处的内容就应该位于`0x8000 + 0x4200 = 0xc200`号地址。

这样的话，我们就往`haribote.nas`里加上`ORG 0xc200`，然后在`ipl.nas`处理的最后加上`JMP 0xc200`。

```nasm title="ipl.nas" hl_lines="70"
CYLS    EQU    10
        ORG    0x7c00

        JMP    entry
        DB     0x90
        DB     "HARIBOTE"
        DW     512
        DB     1
        DW     1
        DB     2
        DW     224
        DW     2880
        DB     0xf0
        DW     9
        DW     18
        DW     2
        DD     0
        DD     2880
        DB     0,0,0x29
        DD     0xffffffff
        DB     "HARIBOTEOS "
        DB     "FAT12   "
        RESB   "18"

entry:
        MOV    AX,0
        MOV    SS,AX
        MOV    SP,0x7c00
        MOV    DS,AX
        MOV    AX,0x0820
        MOV    ES,AX
        MOV    CH,0
        MOV    DH,0
        MOV    CL,2

readloop:
        MOV    SI,0

retry:
        MOV    AH,0x02
        MOV    AL,1
        MOV    BX,0
        MOV    DL,0x00
        INT    0x13
        JNC    next
        ADD    SI,1
        CMP    SI,5
        JAE    error
        MOV    AH,0x00
        MOV    DL,0x00
        INT    0x13
        JMP    retry

next:
        MOV    AX,ES
        ADD    AX,0x0020
        MOV    ES,AX
        ADD    CL,1
        CMP    CL,18
        JBE    readloop
        MOV    CL,1
        ADD    DH,1
        CMP    DH,2
        JB     readloop
        MOV    DH,0
        ADD    CH,1
        CMP    CH,CYLS
        JB     readloop

		JMP    0xc200

error:
        MOV    SI,msg

putloop:
        MOV    AL,[SI]
        ADD    SI,1
        CMP    AL,0
        JE     fin
        MOV    AH,0x0e
        MOV    BX,15
        INT    0x10
        JMP    putloop

fin:
        HLT
        JMP    fin
        
msg:
        DB     0x0a,0x0a
        DB     "load error"
        DB     0x0a
        DB     0
        RESB   0x7dfe-$
        DB     0x55,0xaa
```

```nasm title="haribote.nas"
	ORG    0xc200

fin:
	HLT
	JMP    fin
```

## 确认操作系统的执行情况

考虑到将来我们肯定要做成Windows那样的画面，所以这次就来切换一下画面模式。

```nasm title="haribote.nas"
	ORG    0xc200
	MOV    AL,0x13       ; VGA显卡，320x200x8位彩色
	MOV    AH,0x00
	INT    0x10

fin:
	HLT
	JMP    fin
```

设定`AH=0x00`后，调用显卡BIOS的函数，都这样就可以切换显示模式了。

![image.png](https://vs-picbed-1320307070.cos.ap-nanjing.myqcloud.com/img/20240221211509.png)

另外，想要把磁盘装载内容的结束地址告诉给`haribote.sys`，所以在`JMP 0xc200`之前，我们将`CYLS`的值写到内存地址`0x0ff0`中。

## 32位模式前期准备

16位模式和32位模式中，机器语言的命令代码不一样。同样的机器语言，解释的方法不一样，所以16位模式的机器语言在32位模式下不能运行，反之亦然😭。

由于BIOS是用16位机器语言写的。如果我们有什么事情想用BIOS做，那就全部放在开头，因为一旦进入32位模式就不能调用BIOS函数了。

接下来还想从BIOS得到键盘状态。

```nasm title="haribote.nas"
; 有关BOOT_INFO
CYLS    EQU    0x0ff0    ; 设定启动区
LEDS    EQU    0x0ff1    
VMODE   EQU    0x0ff2    ; 颜色的位数
SCRNX   EQU    0x0ff4    ; 分辨率的X
SCRNX   EQU    0x0ff6    ; 分辨率的Y
VRAM    EQU    0x0ff8    ; 图像缓冲区的开始地址

	    ORG    0xc200
	    MOV    AL,0x13   ; VGA显卡...
		MOV    AH,0x00
		INT    0x10
		MOV    BYTE  [VMODE],8
		MOV    WORD  [SCRNX],320
		MOV    WORD  [SCRNY],200
		MOV    DWORD [VRAM], 0x000a0000

; 用BIOS取得键盘上各种LED指示灯的状态。
		MOV    AH,0x02
		INT    0x16      ; keyboard BIOS
		MOV    [LEDS],AL

fin:
		HLT
		JMP    fin
```

设置画面模式之后，还把画面模式的信息保存在了内存里。以后我们可能要支持各种不同的画面模式，这就需要把现在的设置信息保存起来以备后用。

我们暂且将启动时的信息称为BOOT_INFO。

### `VRAM`

`VRAM`指的是显卡内存，也就是用来显示画面的内存。它的各个地址都对应着画面上的像素。

`VRAM`分布在内存分布图上好几个不同的地方。这是因为i，不同画面模式的像素数也不一样。

在`INT 0x10`画面模式下，`VRAM`是`0xa0000~0xaffff`的64KB。

## 开始导入C语言

终于准备就绪，现在我们直接切换到C语言。

`haribote.sys`改名为`asmhead.nas`，并且，为了调用C语言写的程序，添加了汇编代码。

```nasm title="asmhead.nas"

```

```c title="bootpack.c"
void HariMain(void)
{
fin:
	goto fin;
	//这里想写上HLT，但C语言不能用HLT
}
```

`goto`相当于汇编语言中的`JMP`，实际上也是被翻译成`JMP`指令。

### C语言怎样变成机器语言

那么这个`bootpack.c`是怎样变成机器语言的呢🤔？

1. 使用`cc1.exe`从`bootpack.c`生成`bootpack.gas`
2. 使用`gas2nask.exe`从`bootpack.exe`生成`bootpack.nas`
3. 使用`nask.exe`从`bootpack.exe`生成`bootpack.obj`
4. 使用`obi2bim.exe`从`bootpack.exe`生成`bootpack.bim`
5. 使用`bim2hrb.exe`从`bootpack.bin`生成`bootpack.hrb`

`cc1`由`gcc`改造而来，`gcc`以`gas`汇编语言为基础，不能翻译成`nask`。所以我们需要把`gas`变换成`nask`能翻译的语法，这就是`gas2nask.exe`。

一旦转换成`nas`文件，只要用`nask`翻译一下，就能变成机器语言了。

### 目标文件

首先用`nask`制作`obj`文件，又称为目标文件。因为我们的程序是C语言写的，目标是机器语言。

目标文件是一种特殊的机器语言文件，必须与其他文件链接后才能变成真正可以执行的机器语言。

C语言有它的局限性，不可能只用C语言来编写所有的程序，其中有一部分必须用汇编语言来写，然后**链接**到C语言写的程序上。这也是C语言的不便之处——不能直接把它写进映像文件。

单个的目标文件还不是独立的机器语言，其中还有一部分是没完成的。==为了能做成完整的机器语言，必须将必要的目标文件全部链接上==。完成这项工作的是`obj2bim`。

`bim`是作者自创的一种文件格式，意思为`binary image`，它是一种二进制映像文件。

#### 映像文件

磁盘映像也是一种映像文件。所谓映像文即不是文件本来的状态，而是一种替代形式。

所以，实际上`bim`文件也不是完成品，它只是将各个部分全部都链接在仪器，做成了一个完整的机器语言文件。

而为了能实际使用，我们还需要针对每一个不同操作系统的要求**进行必要的加工**。必须加上识别用的文件头，或者压缩等。

这样做的好处就是仅靠这个编译器，就可以制作Windows、Linux、OSASK、haribote用的可执行文件。